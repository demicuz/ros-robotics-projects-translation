# 4.8. Управление светом с помощью Arduino и ROS

Мы можем начать кодировать базовый узел Arduino-ROS, который может воспринимать количество света с помощью светозависимого резистор \(LDR\). Вы можете использовать любой Arduino для этой демонстрации; здесь мы будем использовать Arduino Mega 2560. Приведенная на следующем рисунке схема LDR с Ардуино. Характеристика LDR заключается в том, что это в основном резистор, сопротивление которого меняется, когда на него падает свет. Максимальное сопротивление - когда никакого света и минимум, когда свет падает на него.

![](../.gitbook/assets/image%20%2815%29.png)

Мы должны подключить один вывод 5В от платы Arduino, а следующий вывод Arduino A0. Эта клемма соединена с выводом GND через резистор 10 кОм. Это схема делителя напряжения. Уравнение для нахождения напряжения в точке А0 выглядит следующим образом:

$$
V_a0 = 5 * (R2 / (R1 + R2))
$$

Из уравнения ясно, что при отсутствии света мы получим минимальное напряжение, а когда будет свет, мы получим максимум. Это значение может быть считано с помощью Arduino.

Вот код для ROS, который считывает LDR:

```text
#include <Arduino.h>
#include <ros.h>
#include <rosserial_arduino/Adc.h>

ros::NodeHandle nh;
rosserial_arduino::Adc adc_msg;
ros::Publisher p("adc", &adc_msg);

void setup()
{
    nh.initNode();
    nh.advertise(p);
}

//We average the analog reading to elminate some of the noise
int averageAnalog(int pin)
{
    int v=0;
    for(int i=0; i<4; i++) v+= analogRead(pin);
    return v/4;
}

long adc_timer;

void loop()
{
    adc_msg.adc0 = averageAnalog(0);
    p.publish(&adc_msg);
    nh.spinOnce();
    delay(50);
}
```

Вот объяснение кода:

```text
#include <Arduino.h>
#include <ros.h>
#include <rosserial_arduino/Adc.h>
```

`<Arduino.h>` содержит определения специфичных для Arduino функций. `<ros.h>` библиотека содержит функциональные возможности клиента Arduino-to-ROS. `<rosserial_arduino/Adc.h>` заголовок содержит определения сообщений для переноса нескольких значений ADC в одном сообщении.

```text
ros::NodeHandle nh;
```

Это создает Ros nodehandle. Как и другие узлы ROS, мы используем этот дескриптор для публикации и подписи на Arduino.

```text
rosserial_arduino::Adc adc_msg;
ros::Publisher p("adc", &adc_msg);
```

Этот код создаст экземпляр `adc_msg` и создаст объект publisher.

```text
void setup()
{
    nh.initNode();
    nh.advertise(p);
}
```

Это инициализирует узел и свяжет объект publisher, чтобы начать публикацию вызванного раздела `/abc`.

```text
void loop()
{
    adc_msg.adc0 = averageAnalog(0);
    p.publish(&adc_msg);
    nh.spinOnce();
    delay(50);
}
```

В цикле считывается аналоговое значение из вывода A0 и вычисляется среднее значение. Средняя величина значение будет опубликовано в разделе `/adc`.

После компиляции кода, вы можете выбрать плату из **Tool \| Board** и **Serial Port**. Теперь вы можете записать код на плате Arduino.



